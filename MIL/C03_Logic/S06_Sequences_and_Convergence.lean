import MIL.Common
import Mathlib.Data.Real.Basic

namespace C03S06

def ConvergesTo (s : ℕ → ℝ) (a : ℝ) :=
  ∀ ε > 0, ∃ N, ∀ n ≥ N, |s n - a| < ε

example : (fun x y : ℝ ↦ (x + y) ^ 2) = fun x y : ℝ ↦ x ^ 2 + 2 * x * y + y ^ 2 := by
  ext
  ring

example (a b : ℝ) : |a| = |a - b + b| := by
  congr
  ring

example {a : ℝ} (h : 1 < a) : a < a * a := by
  convert (mul_lt_mul_right _).2 h
  · rw [one_mul]
  exact lt_trans zero_lt_one h

theorem convergesTo_const (a : ℝ) : ConvergesTo (fun x : ℕ ↦ a) a := by
  intro ε εpos
  use 0
  intro n nge
  rw [sub_self, abs_zero]
  apply εpos

theorem convergesTo_add {s t : ℕ → ℝ} {a b : ℝ}
      (cs : ConvergesTo s a) (ct : ConvergesTo t b) :
    ConvergesTo (fun n ↦ s n + t n) (a + b) := by
  intro ε εpos
  dsimp -- this line is not needed but cleans up the goal a bit.
  have ε2pos : 0 < ε / 2 := by linarith
  rcases cs (ε / 2) ε2pos with ⟨Ns, hs⟩
  rcases ct (ε / 2) ε2pos with ⟨Nt, ht⟩
  use max Ns Nt
  intro n hn
  have hsn: |s n - a| < ε / 2 := by
    apply hs
    exact le_of_max_le_left hn
  have htn: |t n - b| < ε / 2 := by
    apply ht
    exact le_of_max_le_right hn
  have h: |s n + t n - (a+b)| ≤ |s n - a| + |t n - b| := by
    rw [add_sub_add_comm]
    exact abs_add_le (s n - a) _
  have h1: |s n - a| + |t n - b| < ε/2 + ε / 2 := by
    apply add_lt_add hsn htn
  have h2: |s n - a| + |t n - b| < ε := by
    linarith [h1]
  exact lt_of_le_of_lt h h2




theorem convergesTo_mul_const {s : ℕ → ℝ} {a : ℝ} (c : ℝ) (cs : ConvergesTo s a) :
    ConvergesTo (fun n ↦ c * s n) (c * a) := by
  by_cases h : c = 0
  · convert convergesTo_const 0
    · rw [h]
      ring
    rw [h]
    ring
  have acpos : 0 < |c| := abs_pos.mpr h
  intro ε εpos
  have εcpos : ε/|c| > 0 := div_pos εpos acpos
  rcases cs (ε/|c|) εcpos with ⟨Ns, hs⟩
  use Ns
  intro n nge
  simp
  rw [← mul_sub, abs_mul]
  apply (lt_div_iff₀' acpos).mp
  apply hs
  assumption



theorem exists_abs_le_of_convergesTo {s : ℕ → ℝ} {a : ℝ} (cs : ConvergesTo s a) :
    ∃ N b, ∀ n, N ≤ n → |s n| < b := by
  rcases cs 1 zero_lt_one with ⟨N, h⟩
  use N, |a| + 1
  intro n hn
  apply h at hn
  calc
    |s n| = |s n| - |a| + |a| := by ring
    _ ≤ |s n - a| + |a| := by
      apply add_le_add
      apply abs_sub_abs_le_abs_sub
      apply le_refl
    _ ≤ |a| + |s n - a| := by
      rw [add_comm]
    _ < |a| + 1 := by
      apply (add_lt_add_iff_left |a|).mpr hn


theorem aux {s t : ℕ → ℝ} {a : ℝ} (cs : ConvergesTo s a) (ct : ConvergesTo t 0) :
    ConvergesTo (fun n ↦ s n * t n) 0 := by
  intro ε εpos
  dsimp
  rcases exists_abs_le_of_convergesTo cs with ⟨N₀, B, h₀⟩
  have Bpos : 0 < B := lt_of_le_of_lt (abs_nonneg _) (h₀ N₀ (le_refl _))
  have Bnot0 : B ≠ 0 := by
    apply ne_of_gt Bpos
  have pos₀ : ε / B > 0 := div_pos εpos Bpos
  rcases ct _ pos₀ with ⟨N₁, h₁⟩
  use max N₀ N₁
  intro n hn
  have Beps : B * (ε / B) = ε := by
    apply mul_div_cancel₀
    apply Bnot0
  calc
    |s n * t n - 0| = |s n * t n| := by norm_num
    _ = |s n| * |t n| := by apply abs_mul
    _ ≤ B * |t n| := by
      apply mul_le_mul
      apply le_of_lt
      apply h₀
      apply le_of_max_le_left hn
      apply le_refl
      apply abs_nonneg
      apply le_of_lt Bpos
    _ < B * (ε / B) := by
      rw [← sub_zero (t n)]
      apply (mul_lt_mul_left Bpos).mpr
      apply h₁
      apply le_of_max_le_right hn
    _ = ε := by
      rw [Beps]

theorem convergesTo_mul {s t : ℕ → ℝ} {a b : ℝ}
      (cs : ConvergesTo s a) (ct : ConvergesTo t b) :
    ConvergesTo (fun n ↦ s n * t n) (a * b) := by
  have h₁ : ConvergesTo (fun n ↦ s n * (t n + -b)) 0 := by
    apply aux cs
    convert convergesTo_add ct (convergesTo_const (-b))
    ring
  have := convergesTo_add h₁ (convergesTo_mul_const b cs)
  convert convergesTo_add h₁ (convergesTo_mul_const b cs) using 1
  · ext; ring
  ring

theorem convergesTo_unique {s : ℕ → ℝ} {a b : ℝ}
      (sa : ConvergesTo s a) (sb : ConvergesTo s b) :
    a = b := by
  by_contra abne
  have : |a - b| > 0 := by
    by_cases h: a - b = 0
    apply sub_eq_zero.mp at h
    contradiction
    apply abs_sub_pos.mpr abne
  let ε := |a - b| / 2
  have εpos : ε > 0 := by
    change |a - b| / 2 > 0
    linarith
  rcases sa ε εpos with ⟨Na, hNa⟩
  rcases sb ε εpos with ⟨Nb, hNb⟩
  let N := max Na Nb
  have absa : |s N - a| < ε := by
    apply hNa
    apply le_max_left
  have absb : |s N - b| < ε := by
    apply hNb
    apply le_max_right
  have : |a - b| < |a - b| :=
  calc
    |a - b| = |(-(s N - a)) + (s N - b)| := by
      congr
      ring
    _ ≤ |-(s N - a)| + |s N - b| := by
      apply abs_add_le
    _ = |s N - a| + |s N - b| := by
      rw [abs_neg]
    _ < ε + ε := by
      apply add_lt_add absa absb
    _ = |a-b| := by
      norm_num [ε]
  exact lt_irrefl _ this

section
variable {α : Type*} [LinearOrder α]

def ConvergesTo' (s : α → ℝ) (a : ℝ) :=
  ∀ ε > 0, ∃ N, ∀ n ≥ N, |s n - a| < ε

end
